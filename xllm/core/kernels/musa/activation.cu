/* Copyright 2025 The vLLM Authors and The xLLM Authors. All Rights Reserved.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://github.com/jd-opensource/xllm/blob/main/LICENSE

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
==============================================================================*/
#include <ATen/ops/mv.h>
#include <ATen/ops/swish_glu.h>
#include <c10/cuda/CUDAException.h>
#include <c10/cuda/CUDAGuard.h>
#include <torch/cuda.h>

#include <cstdint>
#include <vector>

#include "core/kernels/cuda/device_utils.cuh"
#include "core/kernels/musa/musa_ops_api.h"

namespace {

using ::xllm::kernel::cuda::xllm_ldg;

template <typename scalar_t,
          scalar_t (*ACT_FN)(const scalar_t&),
          bool act_first>
__device__ __forceinline__ scalar_t compute(const scalar_t& x,
                                            const scalar_t& y) {
  return act_first ? ACT_FN(x) * y : x * ACT_FN(y);
}

__device__ __forceinline__ bool is_16byte_aligned(const void* ptr) {
  return (reinterpret_cast<uintptr_t>(ptr) & 15) == 0;
}

inline bool is_16byte_aligned_host(const void* ptr) {
  return (reinterpret_cast<uintptr_t>(ptr) & 15) == 0;
}

template <typename scalar_t,
          scalar_t (*ACT_FN)(const scalar_t&),
          bool act_first>
__global__ void XLLM_KERNEL_ATTR(1024)
    act_and_mul_kernel(scalar_t* __restrict__ out,
                       const scalar_t* __restrict__ input,
                       const int32_t d) {
  constexpr int32_t kVecSize = 16 / sizeof(scalar_t);
  const int64_t token_idx = blockIdx.x;
  const scalar_t* x_ptr = input + token_idx * 2 * d;
  const scalar_t* y_ptr = x_ptr + d;
  scalar_t* out_ptr = out + token_idx * d;

  const bool aligned = is_16byte_aligned(x_ptr) && is_16byte_aligned(y_ptr) &&
                       is_16byte_aligned(out_ptr);

  if (aligned && d >= kVecSize) {
    const int4* x_vec = reinterpret_cast<const int4*>(x_ptr);
    const int4* y_vec = reinterpret_cast<const int4*>(y_ptr);
    int4* out_vec = reinterpret_cast<int4*>(out_ptr);
    const int32_t num_vecs = d / kVecSize;
    const int32_t vec_end = num_vecs * kVecSize;

    for (int32_t i = threadIdx.x; i < num_vecs; i += blockDim.x) {
      int4 x = xllm_ldg(&x_vec[i]), y = xllm_ldg(&y_vec[i]), r;
      auto* xp = reinterpret_cast<scalar_t*>(&x);
      auto* yp = reinterpret_cast<scalar_t*>(&y);
      auto* rp = reinterpret_cast<scalar_t*>(&r);
#pragma unroll
      for (int32_t j = 0; j < kVecSize; j++) {
        rp[j] = compute<scalar_t, ACT_FN, act_first>(xp[j], yp[j]);
      }
      out_vec[i] = r;
    }
    for (int32_t i = vec_end + threadIdx.x; i < d; i += blockDim.x) {
      out_ptr[i] = compute<scalar_t, ACT_FN, act_first>(xllm_ldg(&x_ptr[i]),
                                                        xllm_ldg(&y_ptr[i]));
    }
  } else {
    for (int64_t idx = threadIdx.x; idx < d; idx += blockDim.x) {
      const scalar_t x = xllm_ldg(&x_ptr[idx]);
      const scalar_t y = xllm_ldg(&y_ptr[idx]);
      out_ptr[idx] = compute<scalar_t, ACT_FN, act_first>(x, y);
    }
  }
}

template <typename T>
__device__ __forceinline__ T silu_kernel(const T& x) {
  const float f = static_cast<float>(x);
  return static_cast<T>(f / (1.0f + expf(-f)));
}

template <typename T>
__device__ __forceinline__ T gelu_kernel(const T& x) {
  const float f = static_cast<float>(x);
  constexpr float kAlpha = M_SQRT1_2;
  return static_cast<T>(f * 0.5f * (1.0f + ::erf(f * kAlpha)));
}

template <typename T>
__device__ __forceinline__ T gelu_tanh_kernel(const T& x) {
  const float f = static_cast<float>(x);
  constexpr float kBeta = M_SQRT2 * M_2_SQRTPI * 0.5f;
  constexpr float kKappa = 0.044715;
  float x_cube = f * f * f;
  float inner = kBeta * (f + kKappa * x_cube);
  return static_cast<T>(0.5f * f * (1.0f + ::tanhf(inner)));
}

#define LAUNCH_ACTIVATION_GATE_KERNEL(KERNEL, ACT_FIRST)                   \
  int32_t d = static_cast<int32_t>(input.size(-1) / 2);                    \
  int64_t num_tokens = input.numel() / input.size(-1);                     \
  dim3 grid(num_tokens);                                                   \
  dim3 block(std::min<int32_t>(d, 1024));                                  \
  if (num_tokens == 0) {                                                   \
    return;                                                                \
  }                                                                        \
  const at::cuda::OptionalCUDAGuard device_guard(device_of(input));        \
  const cudaStream_t stream = at::cuda::getCurrentCUDAStream();            \
  DISPATCH_FLOATING_TYPES(input.scalar_type(), "act_and_mul_kernel", [&] { \
    act_and_mul_kernel<scalar_t, KERNEL<scalar_t>, ACT_FIRST>              \
        <<<grid, block, 0, stream>>>(                                      \
            out.data_ptr<scalar_t>(), input.data_ptr<scalar_t>(), d);      \
  });                                                                      \
  C10_CUDA_KERNEL_LAUNCH_CHECK();

void silu_and_mul(torch::Tensor out, torch::Tensor input) {
  LAUNCH_ACTIVATION_GATE_KERNEL(silu_kernel, true);
}

void gelu_and_mul(torch::Tensor& out, torch::Tensor& input) {
  LAUNCH_ACTIVATION_GATE_KERNEL(gelu_kernel, true);
}

void gelu_tanh_and_mul(torch::Tensor& out, torch::Tensor& input) {
  LAUNCH_ACTIVATION_GATE_KERNEL(gelu_tanh_kernel, true);
}

template <typename scalar_t>
__global__ void XLLM_KERNEL_ATTR(1024)
    mul_sigmoid_gate_strided_2d_kernel(scalar_t* __restrict__ out,
                                       const scalar_t* __restrict__ gate,
                                       const int64_t n,
                                       const int64_t out_row_stride,
                                       const int64_t gate_row_stride) {
  const int64_t row = blockIdx.x;
  scalar_t* out_row = out + row * out_row_stride;
  const scalar_t* gate_row = gate + row * gate_row_stride;
  for (int64_t col = threadIdx.x; col < n; col += blockDim.x) {
    const float g = static_cast<float>(xllm_ldg(&gate_row[col]));
    const float s = 1.0f / (1.0f + expf(-g));
    out_row[col] = static_cast<scalar_t>(static_cast<float>(out_row[col]) * s);
  }
}

void launch_mul_sigmoid_gate_inplace(torch::Tensor& out,
                                     const torch::Tensor& gate) {
  const int64_t n = out.numel();
  if (n == 0) {
    return;
  }
  const at::cuda::OptionalCUDAGuard device_guard(device_of(out));
  const cudaStream_t stream = at::cuda::getCurrentCUDAStream();

  const int64_t last_dim = out.size(-1);
  const int64_t M = n / last_dim;
  const int64_t out_row_stride = (out.dim() <= 1) ? last_dim : out.stride(-2);
  const int64_t gate_row_stride =
      (gate.dim() <= 1) ? last_dim : gate.stride(-2);

  const int32_t threads =
      static_cast<int32_t>(std::min<int64_t>(last_dim, 1024));
  dim3 grid(static_cast<unsigned int>(M));
  dim3 block(static_cast<unsigned int>(threads));
  DISPATCH_FLOATING_TYPES(out.scalar_type(), "mul_sigmoid_gate_inplace", [&] {
    mul_sigmoid_gate_strided_2d_kernel<scalar_t>
        <<<grid, block, 0, stream>>>(out.data_ptr<scalar_t>(),
                                     gate.data_ptr<scalar_t>(),
                                     last_dim,
                                     out_row_stride,
                                     gate_row_stride);
  });
  C10_CUDA_KERNEL_LAUNCH_CHECK();
}

constexpr int32_t kSharedGateWarpSize = 32;
constexpr int32_t kSharedGateWarpsPerBlock = 8;
constexpr int32_t kSharedGateValuesPerVector = 8;

template <typename scalar_t>
union SharedGateVector {
  int4 packed;
  scalar_t values[kSharedGateValuesPerVector];
};

__device__ __forceinline__ float shared_gate_warp_sum(float value) {
#pragma unroll
  for (int32_t offset = kSharedGateWarpSize / 2; offset > 0; offset >>= 1) {
    value += __shfl_xor_sync(0xffffffffu, value, offset);
  }
  return value;
}

__device__ __forceinline__ float stable_sigmoid(float value) {
  const bool positive = value >= 0.0f;
  const float exponential = __expf(positive ? -value : value);
  const float reciprocal = 1.0f / (1.0f + exponential);
  return positive ? reciprocal : exponential * reciprocal;
}

template <typename scalar_t>
__global__ void __launch_bounds__(kSharedGateWarpsPerBlock* kSharedGateWarpSize,
                                  1)
    fused_shared_expert_gate_kernel(scalar_t* __restrict__ shared_output,
                                    const scalar_t* __restrict__ hidden_states,
                                    const scalar_t* __restrict__ gate_weight,
                                    int64_t num_tokens,
                                    int64_t hidden_size) {
  const int32_t warp = threadIdx.x / kSharedGateWarpSize;
  const int32_t lane = threadIdx.x % kSharedGateWarpSize;
  const int64_t token =
      static_cast<int64_t>(blockIdx.x) * kSharedGateWarpsPerBlock + warp;
  if (token >= num_tokens) {
    return;
  }

  const int64_t vector_count = hidden_size / kSharedGateValuesPerVector;
  const int64_t token_base = token * hidden_size;
  float gate_value = 0.0f;
  for (int64_t vector = lane; vector < vector_count;
       vector += kSharedGateWarpSize) {
    const int64_t offset = token_base + vector * kSharedGateValuesPerVector;
    SharedGateVector<scalar_t> hidden_vector;
    SharedGateVector<scalar_t> weight_vector;
    hidden_vector.packed =
        *reinterpret_cast<const int4*>(hidden_states + offset);
    weight_vector.packed = *reinterpret_cast<const int4*>(
        gate_weight + vector * kSharedGateValuesPerVector);
#pragma unroll
    for (int32_t index = 0; index < kSharedGateValuesPerVector; ++index) {
      gate_value += static_cast<float>(hidden_vector.values[index]) *
                    static_cast<float>(weight_vector.values[index]);
    }
  }

  gate_value = shared_gate_warp_sum(gate_value);
  const float gate = stable_sigmoid(
      __shfl_sync(0xffffffffu, gate_value, 0, kSharedGateWarpSize));
  for (int64_t vector = lane; vector < vector_count;
       vector += kSharedGateWarpSize) {
    const int64_t offset = token_base + vector * kSharedGateValuesPerVector;
    SharedGateVector<scalar_t> output_vector;
    output_vector.packed =
        *reinterpret_cast<const int4*>(shared_output + offset);
#pragma unroll
    for (int32_t index = 0; index < kSharedGateValuesPerVector; ++index) {
      output_vector.values[index] = static_cast<scalar_t>(
          static_cast<float>(output_vector.values[index]) * gate);
    }
    *reinterpret_cast<int4*>(shared_output + offset) = output_vector.packed;
  }
}

void launch_fused_shared_expert_gate_inplace(torch::Tensor& shared_output,
                                             const torch::Tensor& hidden_states,
                                             const torch::Tensor& gate_weight) {
  const int64_t num_tokens = shared_output.size(0);
  if (num_tokens == 0) {
    return;
  }
  const int64_t hidden_size = shared_output.size(1);
  const int64_t block_count =
      (num_tokens + kSharedGateWarpsPerBlock - 1) / kSharedGateWarpsPerBlock;
  const at::cuda::OptionalCUDAGuard device_guard(device_of(shared_output));
  const cudaStream_t stream = at::cuda::getCurrentCUDAStream();
  DISPATCH_HALF_TYPES(
      shared_output.scalar_type(), "fused_shared_expert_gate", [&] {
        fused_shared_expert_gate_kernel<scalar_t>
            <<<dim3(static_cast<unsigned int>(block_count)),
               dim3(kSharedGateWarpsPerBlock * kSharedGateWarpSize),
               0,
               stream>>>(shared_output.data_ptr<scalar_t>(),
                         hidden_states.data_ptr<scalar_t>(),
                         gate_weight.data_ptr<scalar_t>(),
                         num_tokens,
                         hidden_size);
      });
  C10_CUDA_KERNEL_LAUNCH_CHECK();
}
}  // namespace

namespace xllm::kernel::musa {

void mul_sigmoid_gate_inplace(torch::Tensor& out, const torch::Tensor& gate) {
  CHECK(out.defined() && gate.defined()) << "out and gate must be defined";
  CHECK(out.sizes() == gate.sizes()) << "out and gate must have same shape";
  CHECK(out.scalar_type() == gate.scalar_type()) << "dtype mismatch";
  CHECK(out.device() == gate.device()) << "device mismatch";
  CHECK(out.dim() >= 1) << "out must be at least 1D";
  CHECK(out.stride(-1) == 1 && gate.stride(-1) == 1)
      << "out and gate must have last-dim stride == 1";
  if (out.dim() > 2) {
    const int64_t numel_per_row = out.size(-1);
    CHECK(out.numel() % numel_per_row == 0)
        << "out shape not collapsible to 2D";
    CHECK(gate.numel() % numel_per_row == 0)
        << "gate shape not collapsible to 2D";
  }
  launch_mul_sigmoid_gate_inplace(out, gate);
}

void fused_shared_expert_gate_inplace(torch::Tensor& shared_output,
                                      const torch::Tensor& hidden_states,
                                      const torch::Tensor& gate_weight) {
  CHECK(shared_output.defined() && hidden_states.defined() &&
        gate_weight.defined())
      << "shared_output, hidden_states, and gate_weight must be defined";
  CHECK(shared_output.dim() == 2 && hidden_states.dim() == 2)
      << "shared_output and hidden_states must be 2D";
  CHECK(shared_output.sizes() == hidden_states.sizes())
      << "shared_output and hidden_states must have the same shape";
  CHECK(gate_weight.dim() == 2 && gate_weight.size(0) == 1 &&
        gate_weight.size(1) == hidden_states.size(1))
      << "gate_weight must have shape [1, hidden_size]";
  CHECK(shared_output.scalar_type() == hidden_states.scalar_type() &&
        shared_output.scalar_type() == gate_weight.scalar_type())
      << "shared expert gate dtype mismatch";
  CHECK(shared_output.scalar_type() == torch::kFloat16 ||
        shared_output.scalar_type() == torch::kBFloat16)
      << "shared expert gate only supports FP16 and BF16";
  CHECK(shared_output.device() == hidden_states.device() &&
        shared_output.device() == gate_weight.device())
      << "shared expert gate device mismatch";
  CHECK(shared_output.is_contiguous() && hidden_states.is_contiguous() &&
        gate_weight.is_contiguous())
      << "shared expert gate tensors must be contiguous";
  CHECK(hidden_states.size(1) % kSharedGateValuesPerVector == 0)
      << "shared expert hidden_size must be divisible by "
      << kSharedGateValuesPerVector;
  CHECK(is_16byte_aligned_host(shared_output.data_ptr()) &&
        is_16byte_aligned_host(hidden_states.data_ptr()) &&
        is_16byte_aligned_host(gate_weight.data_ptr()))
      << "shared expert gate tensors must be 16-byte aligned";
  launch_fused_shared_expert_gate_inplace(
      shared_output, hidden_states, gate_weight);
}

namespace {

void act_and_mul(torch::Tensor out,
                 torch::Tensor input,
                 const std::string& act_mode) {
  if (act_mode != "silu" && act_mode != "gelu" && act_mode != "gelu_tanh" &&
      act_mode != "gelu_pytorch_tanh") {
    LOG(FATAL) << "Unsupported act mode: " << act_mode
               << ", only support silu, gelu, gelu_tanh, gelu_pytorch_tanh";
  }

  if (act_mode == "silu") {
    silu_and_mul(out, input);
  } else if (act_mode == "gelu") {
    gelu_and_mul(out, input);
  } else if (act_mode == "gelu_tanh" || act_mode == "gelu_pytorch_tanh") {
    gelu_tanh_and_mul(out, input);
  }
}

}  // namespace

void active(torch::Tensor& output,
            const torch::Tensor& input,
            const std::string& act_mode,
            bool is_gated) {
  CHECK(is_gated) << "MUSA activation requires a gated input.";
  if (act_mode == "silu" || act_mode == "swiglu") {
    output = at::swish_glu(input);
    return;
  }

  CHECK(input.defined()) << "MUSA activation input is undefined.";
  CHECK_GT(input.dim(), 0) << "MUSA activation input must have a dimension.";
  CHECK_EQ(input.size(-1) % 2, 0)
      << "MUSA gated activation input dimension must be even.";
  CHECK(input.is_contiguous()) << "MUSA activation input must be contiguous.";
  std::vector<int64_t> output_shape = input.sizes().vec();
  output_shape.back() /= 2;
  if (!output.defined()) {
    output = torch::empty(output_shape, input.options());
  }
  CHECK(output.sizes().vec() == output_shape)
      << "MUSA activation output shape mismatch.";
  CHECK_EQ(output.scalar_type(), input.scalar_type())
      << "MUSA activation output dtype mismatch.";
  CHECK(output.device() == input.device())
      << "MUSA activation output device mismatch.";
  CHECK(output.is_contiguous()) << "MUSA activation output must be contiguous.";
  act_and_mul(output, input, act_mode);
}

torch::Tensor matmul(torch::Tensor a,
                     torch::Tensor b,
                     std::optional<torch::Tensor> bias,
                     std::optional<torch::Tensor> output_buf) {
  if (output_buf.has_value() && output_buf->defined() && a.dim() == 2 &&
      b.dim() == 2) {
    auto& out = *output_buf;
    const int64_t output_features = b.size(0);
    const bool has_bias = bias.has_value() && bias->defined();
    const bool use_decode_gemv =
        a.size(0) == 1 && a.scalar_type() == torch::kBFloat16 &&
        b.scalar_type() == torch::kBFloat16 && a.size(1) == 2048 &&
        (output_features == 64 || output_features == 256) && !has_bias &&
        a.is_contiguous() && b.is_contiguous() && out.is_contiguous();
    if (use_decode_gemv) {
      auto out_flat = out.view({output_features});
      auto input_flat = a.view({a.size(1)});
      at::mv_out(out_flat, b, input_flat);
      return out;
    }
    auto bt = b.t();
    if (bias.has_value() && bias->defined()) {
      torch::addmm_out(out, *bias, a, bt);
    } else {
      torch::mm_out(out, a, bt);
    }
    return out;
  }
  namespace F = torch::nn::functional;
  return F::linear(a, b, bias.value_or(torch::Tensor()));
}

}  // namespace xllm::kernel::musa
